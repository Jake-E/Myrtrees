plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

def ENV = System.getenv()

configurations {
    shadowCommon
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    mixinConfigs = ["myrtrees-common.mixins.json"]
    useFabricMixin = true
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
    modApi "me.shedaniel:architectury-forge:${rootProject.architectury_version}"

    modRuntime("curse.maven:jei-238222:3192904") {
        transitive = false
    }

    implementation(project(path: ":common")) {
        transitive = false
    }
    developmentForge(project(path: ":common")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveBaseName.set "${rootProject.mod_name}-${project.name}"
    archiveClassifier.set null
}

jar {
    classifier "dev"
}

java {
    withSourcesJar()
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

if (ENV.CURSEFORGE_KEY) {
    curseforge {
        apiKey = ENV.CURSEFORGE_KEY
        project {
            id = project.curseforge_id
            releaseType = project.curseforge_type
            addGameVersion "Forge"
            addGameVersion "1.16.5"
            mainArtifact(remapJar.archivePath)
            relations {
                requiredDependency 'architectury-forge'
            }
            changelog = getGitChangelog
            changelogType = 'markdown'
        }
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = "${rootProject.mod_name}-${project.name}"
            artifact(remapJar)
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        if (ENV.FTB_MAVEN_TOKEN) {
            maven {
                url "https://maven.ftb.dev/release"
                credentials {
                    username = "Jake_E"
                    password = "${ENV.FTB_MAVEN_TOKEN}"
                }
            }
        }

        if (ENV.SAPS_TOKEN) {
            maven {
                url "https://maven.saps.dev/minecraft"
                credentials {
                    username = "Jake_E"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}

rootProject.tasks.getByName("curseforgePublish").dependsOn tasks.getByName("curseforge")
